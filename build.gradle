/*
 * Copyright ish group pty ltd 2020.
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the
 * GNU Affero General Public License version 3 as published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
 * without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU Affero General Public License for more details.
 */

import org.gradle.internal.os.OperatingSystem

buildscript {
	ext {
		log4j2Version = '2.13.3'
		cayenneVersion = '4.1'
		jettyVersion = '9.4.32.v20200930'
        mariadbVersion = '2.7.0'
		derbyVersion = '10.14.2.0'
		googleGuiceVersion = '4.2.2'
		groovyVersion = '2.5.13'
		bouncycastleVersion = '1.59'
		bootiqueVersion = '1.0'
		cfxVersion = '3.3.5'
		jacksonVersion = '2.9.9'
		javaxActivationVersion = '1.2.0'
		webservicesVersion = '94'
		osVersion = OperatingSystem.current().isMacOsX() ? 'mac' : OperatingSystem.current().isWindows() ? 'win' : 'linux'
	}

	repositories {
		maven {
			url 'https://repo.ish.com.au/content/groups/public'
		}
		maven {
			url 'https://repo.ish.com.au/content/groups/ish'
		}
	}

	dependencies {
		classpath "org.apache.cayenne.plugins:cayenne-gradle-plugin:$cayenneVersion"
	}
}

plugins {
	id 'com.github.ben-manes.versions' version '0.33.0' // adds the task 'dependencyUpdates -Drevision=release'
	id 'com.github.node-gradle.node' version '2.2.1' apply false
	id 'com.github.psxpaul.execfork' version '0.1.13' apply false
	id 'com.dorongold.task-tree' version '1.5' // adds the task 'taskTree [some task]'
	id 'application'
}

taskTree{
	noRepeat = true  //do not print a sub-tree in the task-tree more than once
}

apply plugin: 'idea'

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'java'
	apply plugin: 'maven-publish'

	repositories {
		maven {
			url 'https://repo.ish.com.au/content/groups/public'
		}
		maven {
			url 'https://repo.ish.com.au/content/groups/ish'
		}
	}
	sourceCompatibility = 11
	// get the version from the command line properties
	version = project.hasProperty('releaseVersion') && project.releaseVersion ? project.releaseVersion : "99-SNAPSHOT"

	if (it.name != 'api-test') {
		dependencies {
			implementation "org.apache.logging.log4j:log4j-api:$log4j2Version"
			implementation "org.apache.logging.log4j:log4j-core:$log4j2Version"
			implementation "org.apache.logging.log4j:log4j-1.2-api:$log4j2Version"
			implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version"
			implementation "org.apache.logging.log4j:log4j-jcl:$log4j2Version"
		}
	}

	dependencies {
		implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
		implementation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"

		implementation 'javax.xml.bind:jaxb-api:2.3.0'
		implementation 'javax.xml.ws:jaxws-api:2.3.0'
		implementation 'javax.annotation:javax.annotation-api:1.3.2'

		testImplementation 'junit:junit:4.13'
		testImplementation 'org.dbunit:dbunit:2.6.0'

		api 'com.google.code.findbugs:jsr305:3.0.2'

		// The following is needed until we upgrade waCommon which is forcing Cayenne back to 4.0M3.2
		configurations.all {
			resolutionStrategy {
				force "org.apache.cayenne:cayenne-server:$cayenneVersion"
			}
		}
    }

	group = 'ish.oncourse.angel'

	publishing {
        repositories {
            maven {
                name = "ish"
                def repo = version.endsWith('SNAPSHOT') ? 'ish-snapshots' : 'ish-releases'
                url = "https://repo.ish.com.au/content/repositories/${repo}"
                credentials {
                    username nexusUsername
                    password nexusPassword
                }
            }
        }
    }

	ext.sharedManifest = manifest {
		attributes 'Release-Version': version,
			'Built-Date': new Date().format("dd-MMM-yyy"),
			'Built-JDK': System.getProperty('java.version'),
			'Built-Gradle': gradle.gradleVersion,
			'Implementation-Version': System.getProperty('build_number') ?: 'local',
			'SCM-Revision': System.getProperty('svn_revision') ?: 'development',
			'Permissions': 'all-permissions',
			'Codebase': '*',
			'Application-Name': 'onCourse',
			'Trusted-Library': 'true'
	}

	test {
		systemProperties 'java.awt.headless': 'true',
		    'java.locale.providers': 'COMPAT,SPI'
		maxHeapSize = '2G'
		maxParallelForks = 1
		ignoreFailures = true

		// log each starting test
		beforeTest {
			descriptor -> logger.lifecycle("Running: " + descriptor)
		}

		reports {
			junitXml.enabled = true
		}
	}
}

task testReport(type: TestReport) {
	destinationDir = file("$buildDir/reports/tests")
	// Include the results from the `test` task in all subprojects
	reportOn subprojects*.test
}

// Skip beta and other non-final releases in the update report
dependencyUpdates.resolutionStrategy {
	componentSelection { rules ->
		rules.all { ComponentSelection selection ->
			boolean rejected = ['alpha', 'beta', 'b', 'rc', 'cr', 'm', 'pr', 'preview', 'ea'].any { qualifier ->
				selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
			}
			if (rejected) {
				selection.reject('Release candidate')
			}
		}
	}
}
