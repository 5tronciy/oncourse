
/*
 * Copyright ish group pty ltd 2020.
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the
 * GNU Affero General Public License version 3 as published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
 * without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU Affero General Public License for more details.
 */

apply plugin: 'java'

dependencies {
    implementation 'org.dbunit:dbunit:2.6.0'
    implementation "org.apache.derby:derby:$derbyVersion"

    testImplementation "com.sun.activation:javax.activation:$javaxActivationVersion"
    testImplementation 'com.intuit.karate:karate-junit4:0.9.5'
    testImplementation 'com.intuit.karate:karate-jersey:0.9.5'
    testImplementation 'com.intuit.karate:karate-core:0.9.5'
    testImplementation 'org.glassfish.jersey.connectors:jersey-jetty-connector:2.26'
    testImplementation "org.eclipse.jetty.http2:http2-http-client-transport:$jettyVersion"
    testImplementation "org.eclipse.jetty:jetty-client:$jettyVersion"
    testImplementation "org.apache.derby:derby:$derbyVersion"
}

sourceCompatibility = 11
targetCompatibility = 11

task copyEmptyDb(type: Copy) {
    from("$rootDir/api-test/src/test/resources/") {
        include 'onCourse.yml'
    }
    into "$rootDir/server/build"

    from zipTree(file("$rootDir/api-test/src/test/resources/onCourse.iocdata.empty.zip"))
    into "$rootDir/server/build"
}

task prepareTestDb(dependsOn: [copyEmptyDb], type: JavaExec) {
    main = 'ish.oncourse.api.test.task.PrepareTestDb'
    classpath = sourceSets.test.runtimeClasspath
    args = ["jdbc:derby:$rootDir/server/build/onCourse.iocdata/onCourse;create=true", "$rootDir/api-test/src/test/resources/test-dataset.xml"]
}

test {
    dependsOn "prepareTestDb"
    dependsOn ":server:startDaemon"
    // pull cucumber options into the cucumber jvm
    systemProperty "cucumber.options", System.properties.getProperty("cucumber.options")
    // pull karate options into the jvm
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    // ensure tests are always run
    outputs.upToDateWhen { false }

}
