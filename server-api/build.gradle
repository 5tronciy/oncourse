apply plugin: 'groovy'


configurations {
    swagger
}

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDir 'src/main/java'
sourceSets.test.java.srcDirs = []
sourceSets.test.groovy.srcDir 'src/test/java'

apply from: "$rootDir/gradle/bootique.gradle"

sourceCompatibility = 11
targetCompatibility = 11

dependencies {

    implementation (project(':common')) {
        exclude group: 'org.bouncycastle'
        exclude module: "hessian"
        exclude group: "org.slf4j", module: "slf4j-simple"
    }

    implementation 'io.swagger:swagger-annotations:1.5.21'
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"

    implementation "org.apache.cxf:cxf-core:$cfxVersion"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxrs:$cfxVersion"

    implementation 'javax.validation:validation-api:1.1.0.Final'
    implementation 'org.apache.bval:org.apache.bval.bundle:1.1.2'

    //latest jackson 2.9.9 conflicts with spring AbstractApplicationContext
    implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.8.7"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.7"

}

sourceSets {
    main {
        groovy {
            srcDirs += "${project.projectDir}/build/generated-sources/src/main/groovy"
        }
    }
}

test {
//    forkEvery = 1 //run every test in separate jvm
//    initTestDBConfig(project, test)
}

tasks.withType(GroovyCompile).configureEach {
    options.incremental = true
}
compileGroovy.groovyOptions.configurationScript = file("${rootDir}/gradle/groovyConfig.groovy")


task swagger (type: Swagger) {
    schema = file("${project.projectDir}/src/main/resources/server-api.yaml")
    schemaVersion = 1
    javaOutput = file("${project.projectDir}/build/generated-sources")
    jsOutput = file("${rootDir}/client-html/build/generated-sources")
}

compileGroovy {
    dependsOn swagger
    groovyClasspath += classpath.filter { it.absolutePath.contains('jaxb-api') }
}

clean.doFirst {
    delete("${project.projectDir}/build/generated-sources")
}
