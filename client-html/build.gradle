apply plugin: 'com.moowork.node'
apply plugin: 'queryLanguageModel'

node {
    version = '9.3.0'
    npmVersion = '5.5.1'
    download = true
}

configurations {
    queryLanguageModelSource
}

dependencies {
    queryLanguageModelSource "ish.oncourse.willow:waCommon:$waCommonWebservicesClientVersion:sources"
}

task increase_heap(type: NodeTask, dependsOn: 'yarn_install') {
    script = project.file('node_modules/.bin/cross-env')
    args = ['NODE_OPTIONS=--max_old_space_size=8192']
}

task webpack(type: NodeTask, dependsOn: 'increase_heap') {
    script = file('node_modules/.bin/webpack')
    args = ['--progress', '-p', '--env.NODE_ENV', 'production', '--env.BUILD_NUMBER', version ?: 'SNAPSHOT']
}

task jest(type: NodeTask, dependsOn: 'yarn_install') {
    script = project.file('node_modules/.bin/jest')
    args = ['--ci', '--testResultsProcessor=jest-junit']
}

task queryGrammar(type: au.com.ish.Aql) {
    outputDir = file("${project.projectDir}/build/generated-sources/aql")
    target = 'JavaScript'
}

sourceSets {
    main.resources.srcDirs 'src/js', 'build/generated-sources'
}

//test.dependsOn(jest)

build.dependsOn(webpack, queryGrammar)

task buildLogin(type: NodeTask, dependsOn: ['yarn_install', ':server-api:swagger']) {
    script = file('node_modules/.bin/webpack')
    args = ['-p', '--progress', '--env.NODE_ENV', 'production', '--config', 'webpack.login.config.js']
}

generateQueryLanguageModel {
    source = zipTree(configurations.queryLanguageModelSource.find { it.name.startsWith("waCommon") })
        .plus(fileTree("$buildDir/../../common/src/main/java"))

    destinationDir = file("$buildDir/generated-sources")
}

processResources.dependsOn(':server-api:swagger', "generateQueryLanguageModel")