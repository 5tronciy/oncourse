apply plugin: 'com.moowork.node'
apply plugin: 'queryLanguageModel'

node {
    version = '9.11.2'
    npmVersion = '6.9.2'
    download = true
}

configurations {
    queryLanguageModelSource
}

dependencies {
    queryLanguageModelSource "ish.oncourse.willow:waCommon:$waCommonWebservicesClientVersion:sources"
}

task webpack_unix(type: NodeTask) {
    script = file('node_modules/.bin/webpack')
    args = ['--env.NODE_ENV', 'production', '--env.BUILD_NUMBER', version ?: 'SNAPSHOT']
}

task webpack_win(type:Exec) {
    workingDir '.'
    executable projectDir.getAbsolutePath() + '\\.gradle\\nodejs\\node-v' + project.extensions.getByName('node').version + '-win-x64\\node.exe'
    args = ['node_modules/webpack/bin/webpack.js/', '--env.NODE_ENV', 'production', '--env.BUILD_NUMBER', 'SNAPSHOT']
}

task jest(type: NodeTask, dependsOn: 'yarn') {
    script = project.file('node_modules/.bin/jest')
    args = ['--ci', '--testResultsProcessor=jest-junit']
}

task queryGrammar(type: YarnTask, dependsOn: 'yarn') {
    args = ['antlr4ts']
}

sourceSets {
    main.resources.srcDirs 'src/js', 'build/generated-sources'
}

//test.dependsOn(jest)

if (osVersion == 'win')
    build.dependsOn(webpack_win, queryGrammar)
else
    build.dependsOn(webpack_unix, queryGrammar)

task buildLogin_unix(type: NodeTask, dependsOn: ['yarn', ':server-api:swagger']) {
    script = file('node_modules/.bin/webpack')
    args = ['--env.NODE_ENV', 'production', '--config', 'webpack.login.config.js']
}

task buildLogin_win(type:Exec, dependsOn: ['yarn', ':server-api:swagger']) {
    workingDir '.'
    executable projectDir.getAbsolutePath() + '\\.gradle\\nodejs\\node-v' + project.extensions.getByName('node').version + '-win-x64\\node.exe'
    args = ['node_modules/webpack/bin/webpack.js/', '--env.NODE_ENV', 'production', '--config', 'webpack.login.config.js']
}

generateQueryLanguageModel {
    source = zipTree(configurations.queryLanguageModelSource.find { it.name.startsWith("waCommon") })
        .plus(fileTree("$buildDir/../../common/src/main/java"))

    destinationDir = file("$buildDir/generated-sources")
}

processResources.dependsOn(':server-api:swagger', "generateQueryLanguageModel")